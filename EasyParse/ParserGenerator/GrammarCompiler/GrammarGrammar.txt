lexemes:
ignore @'[ \t]+'
ignore @'#[^\n]*'
a is @'\->'                                   # arrow
l is 'lexemes:'                               # lexemes keyword
i is 'ignore'                                 # ignore keyword
keywordIs is 'is'                                     # is keyword
rules is 'rules:'                                 # rules keyword
nonTerminal is '[A-Z][a-zA-Z0-9_]*'                     # non-terminal
terminal is @'[a-z_][a-zA-Z0-9_]*'                   # terminal
eoln is @'\n([ \t]*(#[^\n]*)?\n)*'
quotedString is '\'((\\[\\\'nrt])|[^\\\'])*\''
verbatimString is '@\'[^\']*\''

rules:
FullGrammar -> Lexemes rules eoln Grammar
FullGrammar -> eoln FullGrammar
Lexemes -> l eoln
Lexemes -> Lexemes LexemePattern eoln
LexemePattern -> terminal keywordIs String
LexemePattern -> i String
Grammar -> Rule eoln
Grammar -> Grammar Rule eoln
Rule -> nonTerminal a RuleBody
RuleBody -> Symbol
RuleBody -> RuleBody Symbol
Symbol -> terminal
Symbol -> nonTerminal
String -> quotedString
String -> verbatimString
